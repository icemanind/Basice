Tic-Tac-Toe (graphical)
Classic game of Tic-Tac-Toe which demonstrates a lot of features of Basice. This version uses graphics instead of the console screen.
INKEY$(), CLS, PRINT, DIM, FOR/NEXT, IF/THEN/ELSE, CHR$(), ASC(), GOSUB/RETURN, GOTO, END, CURSOR OFF, LOCATE, VAL()
10 CLS: SCREEN 1: CURSOR OFF
20 DIM B$(9):TURN = 1
30 FOR X=1 TO 9:B$(X) = STR$(X) : NEXT
40 GOSUB 1000
50 GOSUB 3000
60 K$=INKEY$():IF K$=CHR$(0) THEN GOTO 60
70 K = ASC(K$)
80 IF K < ASC("1") OR K > ASC("9") THEN GOTO 60
90 B = VAL(K$)
100 IF ASC(B$(B)) >= ASC("1") AND ASC(B$(B)) <= ASC("9") THEN GOTO 115
110 GOSUB 3050: GOTO 60
115 'PRINT "...";
120 IF TURN = 1 THEN B$(B) = "X" ELSE B$(B) = "O"
130 IF TURN = 1 THEN TURN = 0 ELSE TURN = 1
140 GOSUB 4000
150 CLS: GOTO 40
888 PRINT"END":END
1000 ' Draw Tic-Tac-Toe board
1010 SCREEN 1
1020 XOFFSET = 220
1030 YOFFSET = 120
1040 TTTCOLOR = RGB(190,190,255)
1050 LINE XOFFSET+11,YOFFSET,XOFFSET+11,YOFFSET+100,TTTCOLOR
1060 LINE XOFFSET+49,YOFFSET,XOFFSET+49,YOFFSET+100,TTTCOLOR
1070 LINE XOFFSET-20,YOFFSET+30,XOFFSET+20+60,YOFFSET+30,TTTCOLOR
1080 LINE XOFFSET-20,YOFFSET+67,XOFFSET+20+60,YOFFSET+67,TTTCOLOR
1090 GOSUB 2000
1100 SCREEN 2:RETURN
2000 ' Draw X's and O's
2010 FOR X=1 TO 3
2020 DRAWTEXT XOFFSET - 20 + ((X-1) * 36),YOFFSET - 8, B$(X), 25, TTTCOLOR
2040 NEXT
2050 FOR X=4 TO 6
2060 DRAWTEXT XOFFSET - 20 + ((X-4) * 36),YOFFSET + 29, B$(X), 25, TTTCOLOR
2080 NEXT
2090 FOR X=7 TO 9
2100 DRAWTEXT XOFFSET - 20 + ((X-7) * 36),YOFFSET + 65, B$(X), 25, TTTCOLOR
2120 NEXT
3000 ' Draw prompt
3010 IF TURN=1 THEN GOSUB 5000:DRAWTEXT XOFFSET - 65,YOFFSET+120,"It's X's turn. Press a number to claim that space!",11 ELSE GOSUB 5000:DRAWTEXT XOFFSET-65,YOFFSET+120,"It's O's turn. Press a number to claim that space!",15
3020 RETURN
3050 ' Draw occupied prompt
3060 GOSUB 5000:DRAWTEXT XOFFSET - 65,YOFFSET+120,"That space is already occupied! Try another space!          ",15
3080 RETURN
4000 ' Check for a win
4010 W = 0: W$="X": GOSUB 4500
4020 IF W = 1 THEN GOTO 4630
4030 W = 0: W$="O": GOSUB 4500
4040 IF W = 2 THEN GOTO 4630
4050 GOSUB 4400
4060 RETURN
4400 ' Check for cat's game
4410 IF B$(1)<>"1" AND B$(2)<>"2" AND B$(3)<>"3" AND B$(4)<>"4" AND B$(5)<>"5" AND B$(6)<>"6" AND B$(7)<>"7" AND B$(8)<>"8" AND B$(9)<>"9" THEN GOTO 4430
4420 RETURN
4430 CLS
4440 GOSUB 5000:GOSUB 1000:LOCATE 11,3
4450 PRINT "It's a draw!                                                                 "
4460 GOTO 4680
4500 ' Check for win
4510 ' Check for 3 horizontally
4520 IF B$(1) = W$ AND B$(2) = W$ AND B$(3) = W$ THEN W=1:RETURN
4530 IF B$(4) = W$ AND B$(5) = W$ AND B$(6) = W$ THEN W=1:RETURN
4540 IF B$(7) = W$ AND B$(8) = W$ AND B$(9) = W$ THEN W=1:RETURN
4550 ' Check for 3 vertically
4560 IF B$(1) = W$ AND B$(4) = W$ AND B$(7) = W$ THEN W=1:RETURN
4570 IF B$(2) = W$ AND B$(5) = W$ AND B$(8) = W$ THEN W=1:RETURN
4580 IF B$(3) = W$ AND B$(6) = W$ AND B$(9) = W$ THEN W=1:RETURN
4590 ' Check for 3 diagnally
4600 IF B$(1) = W$ AND B$(5) = W$ AND B$(9) = W$ THEN W=1:RETURN
4610 IF B$(3) = W$ AND B$(5) = W$ AND B$(7) = W$ THEN W=1:RETURN
4620 W = 0: RETURN
4630 ' A Win!
4640 CLS
4650 GOSUB 5000: GOSUB 1000
4660 LOCATE 11,3
4670 PRINT W$ + " wins the game!                                                           "
4680 LOCATE 12,3
4690 PRINT "Game Over!"
4700 END
5000 ' Remove Prompt
5010 SCREEN 1
5020 FOR NDY=230 TO 280
5030 LINE 50,NDY,620,NDY,RGB(0,0,0)
5040 NEXT
5050 SCREEN 2
5060 RETURN