Hangman
Classic hangman game. You guess letters until you solve a word puzzle. But you only get 6 guesses!
CLS, PRINT, ASC, VAL, FOR/NEXT, INT, STR$, IF/THEN/ELSE, GOTO, GOSUB/RETURN, LOCATE, LEN, MID$, LEFT$, RIGHT$, RND, READ, DATA
10 CLS: CURSOR OFF: GOSUB 3000: LOCATE 1,1:PRINT " "
20 GUESSES = 0: GUESSEDLETTERS$=""
30 GOSUB 1000
40 GOSUB 2240: LOCATE 13,2
50 PRINT "PLEASE GUESS A LETTER: "
60 A$=INKEY$():IF A$=CHR$(0) THEN GOTO 60
70 IF ASC(A$)<64 THEN GOTO 60
80 IF ASC(A$)>90 AND ASC(A$)<97 THEN GOTO 60
90 IF ASC(A$)>122 THEN GOTO 60
100 FOUND=0
105 IF ASC(A$)>=97 THEN A$=CHR$(ASC(A$)-32)
110 FOR I=1 TO LEN(WORD$)
120 IF A$=MID$(WORD$,I,1) THEN FOUND=1:HIDDENWORD$ = LEFT$(HIDDENWORD$,I-1) + A$ + RIGHT$(HIDDENWORD$,LEN(WORD$)-I)
130 NEXT
140 IF FOUND=0 THEN GUESSES=GUESSES+1
150 IF GUESSES = 6 THEN GOTO 170
155 IF HIDDENWORD$=WORD$ THEN GOTO 220
160 GOTO 30
170 ' PLAYER LOST
180 GOSUB 1000
190 LOCATE 13,2
200 PRINT "SORRY! YOU GOT HANGMAN :( THE WORD WAS: " + WORD$
210 END
220 ' PLAYER WON! YAY!
230 GOSUB 2240: LOCATE 13,2
240 PRINT "CONGRATULATIONS! YOU WON! THE WORD WAS: " + WORD$
999 END
1000 ' DRAW HANGMAN
1010 LOCATE 3,25
1020 PRINT "|"
1030 LOCATE 2,25
1040 PRINT"------"
1050 FOR I = 3 TO 8
1060 LOCATE I,30
1070 PRINT "|"
1080 NEXT I
1090 LOCATE 9,25
1100 PRINT "------"
1110 IF GUESSES = 0 THEN GOTO 1230
1120 IF GUESSES = 1 THEN GOTO 1220
1130 IF GUESSES = 2 THEN GOTO 1210
1140 IF GUESSES = 3 THEN GOTO 1200
1150 IF GUESSES = 4 THEN GOTO 1190
1155 IF GUESSES = 5 THEN GOTO 1180
1160 IF GUESSES = 6 THEN GOTO 1170
1170 GOSUB 2200
1180 GOSUB 2160
1190 GOSUB 2120
1200 GOSUB 2080
1210 GOSUB 2040
1220 GOSUB 2000
1230 RETURN
2000 ' DRAW HEAD
2010 LOCATE 4,25
2020 PRINT "O"
2030 RETURN
2040 ' DRAW TORSO
2050 LOCATE 5,25
2060 PRINT "|"
2061 LOCATE 6,25
2062 PRINT "|"
2070 RETURN
2080 ' DRAW RIGHT ARM
2090 LOCATE 5,26
2100 PRINT "/"
2110 RETURN
2120 ' DRAW LEFT ARM
2130 LOCATE 5,24
2140 PRINT "\"
2150 RETURN
2160 ' DRAW RIGHT LEG
2170 LOCATE 7,26
2180 PRINT "\"
2190 RETURN
2200 ' DRAW LEFT LEG
2210 LOCATE 7,24
2220 PRINT "/"
2230 RETURN
2240 ' DRAW PUZZLE
2250 LOCATE 11,2
2260 PRINT "YOUR WORD: ";
2270 PRINT HIDDENWORD$
2280 RETURN
3000 ' READ IN PUZZLE DATA AND SELECT A RANDOM WORD
3010 NUMWORDS = 20
3020 DIM W$(NUMWORDS)
3030 FOR I=1 TO NUMWORDS
3040 READ W$(I)
3050 NEXT I
3060 WORD$=W$(RND(NUMWORDS))
3070 HIDDENWORD$=""
3080 FOR I=1 TO LEN(WORD$)
3090 HIDDENWORD$=HIDDENWORD$+"-"
3100 NEXT I
3200 RETURN
4000 DATA "CHOCOLATE", "PIZZA", "FISH", "SWORD", "LIMES", "CAT", "DOG", "BIRD", "BOY", "GIRL", "PUPPY", "BASEBALL", "FOOTBALL", "BASEBALLS","FOOTBALLS"
4010 DATA "LION", "TIGERS", "BEER", "SWINE", "EGG"